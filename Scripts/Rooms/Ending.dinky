// ----------------------------------------------------------------------------
//
//  Copyright (c) 2020 Terrible Toybox, Inc. All rights reserved.
//  Licensed for personal use only: https://github.com/grumpygamer/DeloresDev
//
// ----------------------------------------------------------------------------

local SCROLL_TIME = 20.0
local WAIT_TIME = 1.0

script scrollLine(line) {
	local scale = 3.0
	if (line == "$") {
		quitGame()
	}
	if (line == "@floppy") {
		playSound(SOUNDID(floppy_long))
		line = ""
	}
	if (line == "@flush") {
		playSound(SOUNDID(flush_toilet))
		line = ""
	}
	if (sizeof(line) > 0 && line[0] == '+') {
		scale = 4.0
		line = strslice(line, 1)
	} else
	if (sizeof(line) > 0 && line[0] == '-') {
		scale = 2
		line = strslice(line, 1)
	}

	local image
	if (line == "@fmod") {
		image = createImage(UI_SHEET, "fmod")
		imageScale(image, 1.0)
		imageColor(image, 0xbd8cbf)
	} else {
		image = createTextImage(CREDIT_FONT, line, ALIGN_CENTER)
		imageScale(image, scale)
		imageColor(image, 0xbd8cbf)
	}
	imageMoveFromTo(image, LINEAR, SCROLL_TIME, point(SCREEN_CENTER_X, SCREEN_BOTTOM-20), point(SCREEN_CENTER_X, SCREEN_TOP+20))
	imageAlphaFromTo(image, LINEAR, 1.0, 0.0, 1.0)

	breaktime(SCROLL_TIME-1.5)

	imageAlphaFromTo(image, LINEAR, 1.0, 1.0, 0.0)

	breaktime(1.5)

	deleteImage(image)
}

script rollCredits() {
	local credits = loadarray("Credits.txt")

	CREDIT_FONT <- createFont("Savior1.ttf", 16)

	foreach (local line in credits) {
		startthread(scrollLine, line)
		breaktime(WAIT_TIME)
	}

}

script startEnding() {
	local flash_time = 0.75

	setprivatepref("completed_stories", [])
	setprivatepref("bathroom_power", NO)
	syncprefs()

	showGearIcon(NO)
	cutscene(@{
		breakwhiletalking()
		hideInventory(YES)
		stopMusic()
		suspendMusic(YES)
		suspendAmbients(YES)
		Nickel.stopTyping()


		actorTurnTo(delores, DIR_FRONT)
		playSound(SOUNDID(glitch))
		overlayFlashColor(0x80FFFFFF, flash_time)
		sayLine(delores, SAY(11734,"No!"))
		breakwhiletalking()
		sayLine(delores, SAY(11735,"I don't feel well..."))
		breakwhiletalking()
		playSound(SOUNDID(glitch))
		overlayFlashColor(0x80FFFFFF, flash_time)
		sayLine(delores, SAY(11736,"I'm going to get fresh air..."))
		breakwhiletalking()
		actorAt(delores, AStreet.nickelDoor)
		actorFace(delores, DIR_FRONT)
		cameraFollow(delores)
		objectState(AStreet.nickelDoor, "open")
		breaktime(0.25)
		objectState(AStreet.nickelDoor, "closed")
		actorWalkTo(delores, AStreet.dropSpot)
		breakwhilewalking(delores)
		playSound(SOUNDID(glitch))
		overlayFlashColor(0x80FFFFFF, flash_time)
		sayLine(delores, SAY(11737,"Oh no... it's all coming back."))
		breaktime(2.0)
		actorFace(delores, DIR_LEFT)	// Anim is drawn backwards
		actorCostume(delores, "DeloresGetHitAnimation")
		playAnimation(delores, "get_hit")
		playSound(SOUNDID(glitch))
		overlayFlashColor(0x80FFFFFF, flash_time)
		breakwhileanimating(delores)
		breaktime(3.0)
		overlayColorTo(COLOR_BLACK, 6.0)
		breaktime(6.0)
		actorCostume(delores, "DeloresAnimation")

		enterRoom(Ending)
		cameraAt(Ending.ransomeStartSpot)
		overlayColor(COLOR_NONE)

		actorAt(ransome, Ending, Ending.ransomeStartSpot)
		actorOffset(ransome, point(0,30))
		actorScale(ransome, 0.05)
		actorAlpha(ransome, 0.0)
		actorScaleTo(ransome, 1.0, 15.0, EASE_IN)
		actorAlphaTo(ransome, 1.0, 11.0, EASE_IN)
		playAnimation(ransome, "walk_front", YES)
		local drone_sid = playSound(SOUNDID(drone))
		breaktime(15.0)
		actorStand(ransome)
		breaktime(2.0)
		sayLine(ransome, SAY(11738,"So..."))
		breakwhiletalking()
		breaktime(2.0)
		overlayColorTo(COLOR_BLACK, 3.0)
		breaktime(4.0)
		actorAt(ransome, Ending, Ending.ransomeSpot)
		actorAt(delores, Ending, Ending.deloresSpot)
		actorOffset(ransome, point(0,0))
		overlayColorTo(COLOR_NONE, 4.0)
		breaktime(3.0)
		sayLine(ransome, SAY(11739,"...I guess this is all my fault."))
		breakwhiletalking()
		breaktime(2.0)
		sayLine(ransome, SAY(11740,"It must have been a bug in my hacked savegame."))
		breakwhiletalking()
		breaktime(2.0)
		sayLine(ransome, SAY(11741,"I don't have the same skills you do."))
		breakwhiletalking()
		breaktime(0.5)
		sayLine(delores, SAY(11742,"Who does."))
		breakwhiletalking()
		breaktime(1.0)
		sayLine(ransome, SAY(11743,"You weren't supposed to see through to this side..."), SAY(11744,"...but I guess it's all come crashing down now."))
		breakwhiletalking()
		breaktime(2.0)
		sayLine(delores, SAY(11745,"Let's go see if we can apply a patch..."), SAY(11746,"...I might know just the one."))
		breakwhiletalking()
		overlayColorTo(COLOR_BLACK, 3.0)
		breaktime(5.0)
		delores.footstep_sound = 0
		actorTurnTo(ransome, DIR_BACK)
		actorTurnTo(delores, DIR_BACK)
		actorScaleTo(ransome, 0.0, 15.0, EASE_OUT)
		playAnimation(ransome, "walk_back", YES)
		actorScaleTo(delores, 0.0, 15.0, EASE_OUT)
		playAnimation(delores, "walk_back", YES)

		actorOffsetTo(delores, point(20, 0), 15, EASE_OUT)
		actorOffsetTo(ransome, point(-20, 0), 15, EASE_OUT)

		breaktime(2.0)
		overlayColorTo(COLOR_NONE, 2.0)
		breaktime(5.0)
		actorAlphaTo(ransome, 0.0, 4.0, EASE_OUT)
		actorAlphaTo(delores, 0.0, 4.0, EASE_OUT)

		breaktime(8.0)
		actorAt(ransome, Void, point(0,0))
		actorAt(delores, Void, point(0,0))
		breaktime(3.0)

		stopSound(drone_sid)

		playSound(SOUNDID(credits_music))

		breaktime(3.0)
		breakwhilerunning(startthread(rollCredits))

		breaktime(1000.0)
	})
}

function debugShowCredits() {
	startglobalthread(@{
		//inputOff()
		hideInventory(YES)
		stopMusic()
		suspendMusic(YES)
		showGearIcon(NO)
		enterRoom(Ending)
		cameraAt(Ending.ransomeStartSpot)
		playSound(SOUNDID(credits_music))
		breaktime(3.0)
		breakwhilerunning(startthread(rollCredits))
	})
}

DEFINE_ROOM("Ending", {

	// --------------------------------------------------------------------------------------------
	// General room functions

	// --------------------------------------------------------------------------------------------
	// Enter/Exit code

	enter = function(from_door=null) {
	}

	exit = function() {
	}

	// --------------------------------------------------------------------------------------------
	// Objects

// NEW_OBJECT_HERE
})
